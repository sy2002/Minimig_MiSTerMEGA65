// Copyright 2006, 2007 Dennis van Weeren
//
// This file is part of Minimig
//
// Minimig is free software; you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation; either version 3 of the License, or
// (at your option) any later version.
//
// Minimig is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.
//
//
//
// This is the floppy disk controller (part of Paula)
//
// 23-10-2005	-started coding
// 24-10-2005	-done lots of work
// 13-11-2005	-modified fifo to use block ram
//				-done lots of work
// 14-11-2005	-done more work
// 19-11-2005	-added wordsync logic
// 20-11-2005	-finished core floppy disk interface
//				-added disk interrupts
//				-added floppy control signal emulation
// 21-11-2005	-cleaned up code a bit
// 27-11-2005	-den and sden are now active low (_den and _sden)
//				-fixed bug in parallel/serial converter
//				-fixed more bugs
// 02-12-2005	-removed dma abort function
// 04-12-2005	-fixed bug in fifo empty signalling
// 09-12-2005	-fixed dsksync handling	
//				-added protection against stepping beyond track limits
// 10-12-2005	-fixed some more bugs
// 11-12-2005	-added dout output enable to allow SPI bus multiplexing
// 12-12-2005	-fixed major bug, due error in statemachine, multiple interrupts were requested
//				 after a DMA transfer, this could lock up the whole machine
// 				-enable line disconnected  --> this module still needs a lot of work
// 27-12-2005	-cleaned up code, this is it for now
// 07-01-2005	-added dmas
// 15-01-2006	-added support for track 80-127 (used for loading kickstart)
// 22-01-2006	-removed support for track 80-127 again
// 06-02-2006	-added user disk control input
// 28-12-2006	-spi data out is now low when not addressed to allow multiplexing with multiple spi devices		
//
// JB:
// 2008-07-17	- modified floppy interface for better read handling and write support
//				- spi interface clocked by SPI clock
// 2008-09-24	- incompatibility found: _READY signal should respond to _SELx even when the motor is off
//				- added logic for four floppy drives
// 2008-10-07	- ide command request implementation
// 2008-10-28	- further hdd implementation
// 2009-04-05	- code clean-up
// 2009-05-24	- clean-up & renaming
// 2009-07-21	- WORDEQUAL in DSKBYTR register is always set now
// 2009-11-14 - changed DSKSYNC reset value (Kick 1.3 doesn't initialize this register after reset)
//        - reduced FIFO size (to save some block rams)
// 2009-12-26 - step enable
// 2010-04-12 - implemented work-around for dsksync interrupt request
// 2010-08-14 - set BYTEREADY of DSKBYTR (required by Kick Off 2 loader)

module paula_floppy
(
	// system bus interface
	input         clk,		    	// bus clock
	input         clk7_en,
	input         clk7n_en,
	input         reset,			   // reset 
	input         ntsc,         	// ntsc mode
	input         sof,          	// start of frame
	input	        enable,			// dma enable
	input   [8:1] reg_address_in,	// register address inputs
	input  [15:0] data_in,			// bus data in
	output [15:0] data_out,			// bus data out
	output        dmal,				// dma request output
	output        dmas,				// dma special output

	//disk control signals from cia and user
	input	        _step,				// step heads of disk
	input	        direc,				// step heads direction
	input   [3:0] _sel,				// disk select 	
	input	        side,				// upper/lower disk head
	input	        _motor,			// disk motor control
	output        _track0,			// track zero detect
	output        _change,			// disk has been removed from drive
	output        _ready,			// disk is ready
	output        _wprot,			// disk is write-protected
	output        index,          // disk index pulse

	//interrupt request and misc. control
	output reg    blckint,			// disk dma has finished interrupt
	output        syncint,			// disk syncword found
	input         wordsync,			// wordsync enable

	//HPS I/O interface
	input         IO_ENA,
	input         IO_STROBE,
	output reg    IO_WAIT,
	input  [15:0] IO_DIN,
	output reg [15:0] IO_DOUT,

	output        fdd_led,			//disk activity LED, active when DMA is on
	input	[1:0]   floppy_drives,	//floppy drive number

	// MiSTer2MEGA65 (AExp Amiga 500 port), July 2026: Hardware Floppy support.
	// One drive unit can be backed by the MEGA65's internal floppy drive. The
	// four status lines towards CIA-A are open-collector AND terms across the
	// drives, so for the unit in phys_mask the term of the simulated drive is
	// replaced by the conditioned level of the real pin (still gated by the
	// /SEL of that unit, as a real drive gates its outputs on SELECT). The real
	// INDEX is edge-injected into the CIA-B FLAG source. With phys_mask = 0
	// every expression reduces bit-exactly to the upstream logic. See AExp's
	// doc/developers/hardware-floppy.md, section 4.7 (The CIA side: status
	// lines, ready, index).
	input   [3:0] phys_mask,      // one-hot: which unit is the physical drive (0000 = none)
	input         phys_change_n,  // conditioned real /DSKCHG level (active low)
	input         phys_wprot_n,   // conditioned real /WPROT level (active low)
	input         phys_track0_n,  // conditioned real /TRK0 level (active low)
	input         phys_ready_n,   // synthesized real /RDY level (active low)
	input         phys_index,     // qualified real INDEX level (ms-wide, active high)
	output  [3:0] motor_on_o,     // per-unit latched motor state (sources the real MOTEA)
	output [15:0] fdd_dsig,       // diagnostic: XOR of the first 1024 words stored per
	output  [7:0] fdd_datt,       // track-read attempt + the attempt counter (see below)
	output [15:0] fdd_dc64,       // diagnostic: checkpoint prefixes of the same
	output [15:0] fdd_dc256,      // signature after 64 / 256 stored words
	output [127:0] fdd_dtap,      // diagnostic: the first 8 stored words of the attempt
	output        fdd_dws,        // diagnostic: live ADKCON WORDSYNC level

	// MiSTer2MEGA65 (AExp Amiga 500 port), August 2026: the DSKBYTR
	// observation surface for Rob Northen Copylock, which times the disk by
	// polling DSKBYTR. The upstream DSKBYTR is a constant stub (BYTEREADY and
	// WORDEQUAL always set, data byte 0x00): enough for DMA loaders, but the
	// Copylock loader measures no timing difference and hangs. obs_word/obs_stb
	// carry the word stream of the real drive at true flux pace (tapped in
	// main.vhd where the track engine pops the front-end FIFO); the block at
	// dskbytr below builds a faithful DSKBYTR from it while the physical unit
	// is the selected, motor-on drive and obs_legacy = 0. Every other read
	// returns the upstream stub. See AExp's doc/developers/hardware-floppy.md,
	// section 5 (Copylock and the DSKBYTR observation surface).
	input  [15:0] obs_word,       // reconstructed word from the real drive
	input         obs_stb,        // 1-clk pulse: a new obs_word arrived (clk domain)
	input         obs_legacy,     // 1 = disable the observation surface (A/B: revert to the stub)

	// fifo / track display
	output  [7:0] trackdisp,
	output [13:0] secdisp,
	output        floppy_fwr,
	output        floppy_frd
);

//register names and addresses
parameter DSKBYTR = 9'h01a;
parameter DSKDAT  = 9'h026;		
parameter DSKDATR = 9'h008;
parameter DSKSYNC = 9'h07e;
parameter DSKLEN  = 9'h024;

//local signals
reg  [15:0] dsksync;			//disk sync register
// MiSTer2MEGA65 (AExp Amiga 500 port), June 2026: dsklen was a single 16-bit
// reg driven by two separate always blocks (bits [14:0] and bit [15]/DMAEN) -
// a multi-driven variable that Vivado rejects. Split into one register per
// driving process and recombine with a continuous assign. Semantics unchanged.
//reg  [15:0] dsklen;			//disk dma length, direction and enable (original)
reg  [14:0] dsklen_14_0;	//disk dma length, direction (bits 14:0)
reg         dsklen_15;		//disk dma enable (bit 15, DMAEN)
wire [15:0] dsklen = {dsklen_15, dsklen_14_0};
reg   [6:0] dsktrack[3:0];	//track select
wire  [7:0] track;

reg         dmaon;			//disk dma read/write enabled
wire        lenzero;			//disk length counter is zero
reg         trackwr;			//write track (command to host)
reg         trackrd;			//read track (command to host)

wire        _dsktrack0;		//disk heads are over track 0
wire        dsktrack79;    //disk heads are over track 0

wire [15:0] fifo_in;			//fifo data in
wire [15:0] fifo_out; 		//fifo data out
wire        fifo_wr;			//fifo write enable
reg         fifo_wr_del;	//fifo write enable delayed
wire        fifo_rd;			//fifo read enable
wire        fifo_empty;		//fifo is empty
wire        fifo_full;		//fifo is full
wire [11:0] fifo_cnt;

wire [15:0] dskbytr;			
wire [15:0] dskdatr;

// JB:
wire        fifo_reset;
reg         dmaen;			//dsklen dma enable
reg  [15:0] wr_fifo_status;

reg   [3:0] disk_present;	//disk present status
reg   [3:0] disk_writable;	//disk write access status

wire        _selx;			//active whenever any drive is selected
wire  [1:0] sel;				//selected drive number

reg   [1:0] drives;			//number of currently connected floppy drives (1-4)

reg   [3:0] _disk_change;
reg         _step_del;
reg   [8:0] step_ena_cnt;
wire        step_ena;

// drive motor control
reg  [3:0] _sel_del;       // deleyed drive select signals for edge detection
// MiSTer2MEGA65 (AExp Amiga 500 port), June 2026: motor_on was a single 4-bit
// reg driven by four separate always blocks (one bit each) - a multi-driven
// variable that Vivado rejects. Split into one register per driving process
// and recombine with a continuous assign. Semantics unchanged.
//reg  [3:0] motor_on;       // drive motor on (original)
reg        motor_on_0;     // drive 0 motor on
reg        motor_on_1;     // drive 1 motor on
reg        motor_on_2;     // drive 2 motor on
reg        motor_on_3;     // drive 3 motor on
wire [3:0] motor_on = {motor_on_3, motor_on_2, motor_on_1, motor_on_0};

//decoded commands
reg        cmd_fdd;			//HPS accesses floppy drive buffer

assign     trackdisp = track;
assign     secdisp = dsklen[13:0];

assign     floppy_fwr = fifo_wr;
assign     floppy_frd = fifo_rd;

reg  [1:0] cmd_cnt;

reg stb7;
always @(posedge clk or negedge IO_ENA) begin
	if(~IO_ENA) {IO_WAIT,stb7} <= 0;
	else begin
		if(IO_STROBE) begin
			IO_WAIT <= 1;
		end
		if(clk7_en & IO_WAIT) begin
			if(~stb7) begin
				rx_data <=IO_DIN;
				stb7    <=1;
			end
			if(stb7) begin
				stb7    <=0;
				IO_WAIT <=0;
				IO_DOUT <=tx_data;
			end
		end
	end
end

reg [15:0] rx_data;	//received data from HPS
reg [15:0] tx_data;	//data to be send to HPS

always @(posedge clk or negedge IO_ENA) begin
	if (~IO_ENA) cmd_cnt <= 0;
	else if (clk7_en & stb7 & ~&cmd_cnt) cmd_cnt <= cmd_cnt + 1'd1;
end

//---------------------------------------------------------------------------------------------------------------------

always @(posedge clk) begin
	if (clk7_en) begin
		if (reset | ~IO_ENA)       cmd_fdd <= 0;
		else if (stb7 && !cmd_cnt) cmd_fdd <= (rx_data[15:13]==3'b000 );
	end
end


//transmit data multiplexer
always @(*) begin
	casex ({cmd_cnt, cmd_fdd, trackrd, trackwr})
		
		// fdd request status
		'b00xxx: tx_data = {sel[1:0],drives[1:0],2'b00,trackwr,trackrd&~fifo_cnt[10],track[7:0]};

		// fdd data
		'b01xxx: tx_data = dsksync[15:0];
		'b10x1x: tx_data = {dmaen,dsklen[14:0]};
		'b10x01: tx_data = wr_fifo_status;
		'b1111x: tx_data = {dmaen,dsklen[14:0]};
		'b11101: tx_data = fifo_out;
		
		// no data
		 default: tx_data = 0;
	endcase
end

//floppy disk write fifo status is latched when transmision of the previous word begins 
//it guarantees that when latching the status data into transmit register setup and hold times are met
always @(posedge clk) begin
  if (clk7_en) begin
  	if (stb7)
  		wr_fifo_status <= {dmaen&dsklen[14],3'b000,fifo_cnt[11:0]};
  end
end

//-----------------------------------------------------------------------------------------------//
//active floppy drive number, updated during reset
always @(posedge clk) begin
  if (clk7_en) begin
  	if (reset)
  		drives <= floppy_drives;
  end
end

//-----------------------------------------------------------------------------------------------//
// 300 RPM floppy disk rotation signal
reg [3:0] rpm_pulse_cnt;
always @(posedge clk) begin
  if (clk7_en) begin
    if (sof) begin
      if (rpm_pulse_cnt==11 || !ntsc && rpm_pulse_cnt==9)
        rpm_pulse_cnt <= 0;
      else
        rpm_pulse_cnt <= rpm_pulse_cnt + 4'd1;
    end
  end
end
    
// disk index pulses output
// MiSTer2MEGA65 (AExp Amiga 500 port), July 2026: Hardware Floppy support.
// The simulated 300 RPM index serves only the simulated units; the physical
// unit contributes its real index instead, edge-detected in the clk7_en grid
// (CIA-B FLAG is a negative-edge interrupt on real silicon; cia_int latches
// the level once per clk7 tick, so a one-tick pulse reproduces edge
// semantics), gated like the simulated one on "unit selected + motor on".
//assign index = |(~_sel & motor_on) & ~|rpm_pulse_cnt & sof;  // (original)
reg phys_index_del;
always @(posedge clk) begin
  if (clk7_en) phys_index_del <= phys_index;
end
assign index = (|(~_sel & motor_on & ~phys_mask) & ~|rpm_pulse_cnt & sof) |
               (|(~_sel & motor_on &  phys_mask) & phys_index & ~phys_index_del);
	
//--------------------------------------------------------------------------------------
//data out multiplexer
assign data_out = dskbytr | dskdatr;

//--------------------------------------------------------------------------------------

//active whenever any drive is selected
assign _selx = &_sel[3:0];

// delayed step signal for detection of its rising edge 
always @(posedge clk) begin
  if (clk7_en) begin
    _step_del <= _step;
  end
end

always @(posedge clk) begin
  if (clk7_en) begin
    if (!step_ena)
      step_ena_cnt <= step_ena_cnt + 9'd1;
    else if (_step && !_step_del)
      step_ena_cnt <= 0;
  end
end

assign step_ena = step_ena_cnt[8];

// disk change latch
// set by reset or when the disk is removed form the drive
// reset when the disk is present and step pulse is received for selected drive
always @(posedge clk) begin
  if (clk7_en) begin
    _disk_change <= (_disk_change | ~_sel & {4{_step}} & ~{4{_step_del}} & disk_present) & ~({4{reset}} | ~disk_present);
  end
end
 
//active drive number (priority encoder)
assign sel = !_sel[0] ? 2'd0 : !_sel[1] ? 2'd1 : !_sel[2] ? 2'd2 : !_sel[3] ? 2'd3 : 2'd0;

//delayed drive select signals
always @(posedge clk) begin
  if (clk7_en) begin
    _sel_del <= _sel;
  end
end

//drive motor control
// MiSTer2MEGA65 (AExp Amiga 500 port), June 2026: writes go to the split
// registers motor_on_0..3 (see declaration above).
always @(posedge clk) begin
  if (clk7_en) begin
    if (reset)
      motor_on_0 <= 0;
    else if (!_sel[0] && _sel_del[0])
      motor_on_0 <= ~_motor;
  end
end

always @(posedge clk) begin
  if (clk7_en) begin
    if (reset)
      motor_on_1 <= 0;
    else if (!_sel[1] && _sel_del[1])
      motor_on_1 <= ~_motor;
  end
end

always @(posedge clk) begin
  if (clk7_en) begin
    if (reset)
      motor_on_2 <= 0;
    else if (!_sel[2] && _sel_del[2])
      motor_on_2 <= ~_motor;
  end
end

always @(posedge clk) begin
  if (clk7_en) begin
    if (reset)
      motor_on_3 <= 0;
    else if (!_sel[3] && _sel_del[3])
      motor_on_3 <= ~_motor;
  end
end

//_ready,_track0 and _change signals
// MiSTer2MEGA65 (AExp Amiga 500 port), July 2026: Hardware Floppy support.
// Per-unit source substitution (see the port comment). The AND terms model
// the open-collector bus of a real Amiga: each drive contributes its level
// only while its /SEL is asserted. phys_mask = 0 -> bit-exact originals.
//assign _change = &(_sel | _disk_change);   // (original)
//assign _wprot = &(_sel | disk_writable);   // (original)
wire [3:0] chg_src_n = (~phys_mask & _disk_change ) | (phys_mask & {4{phys_change_n}});
wire [3:0] wp_src_n  = (~phys_mask & disk_writable) | (phys_mask & {4{phys_wprot_n}});
assign _change = &(_sel | chg_src_n);

assign _wprot = &(_sel | wp_src_n);

// the selected unit decides the track0 source: the real /TRK0 sensor for the
// physical unit (trackdisk recalibrates against it; the simulated counter
// can disagree with the real head position), the simulated counter else
//assign  _track0 =&(_selx | _dsktrack0);    // (original)
wire cur_track0_n = phys_mask[sel] ? phys_track0_n : _dsktrack0;
assign  _track0 = _selx | cur_track0_n;

//track control
assign track = {dsktrack[sel],~side};

always @(posedge clk) begin
  if (clk7_en) begin
    if (!_selx && _step && !_step_del && step_ena) begin // track increment (direc=0) or decrement (direc=1) at rising edge of _step
      if (!dsktrack79 && !direc)
        dsktrack[sel] <= dsktrack[sel] + 7'd1;
      else if (_dsktrack0 && direc)
        dsktrack[sel] <= dsktrack[sel] - 7'd1;	
    end
  end
end

// _dsktrack0 detect
assign _dsktrack0 = ~(dsktrack[sel]==0);

// dsktrack79 detect
assign dsktrack79 = dsktrack[sel]==82;

// drive _ready signal control
// Amiga DD drive activates _ready whenever _sel is active and motor is off
// or whenever _sel is active, motor is on and there is a disk inserted (not implemented - _ready is active when _sel is active)
// MiSTer2MEGA65 (AExp Amiga 500 port), July 2026: Hardware Floppy support.
// Rewritten as the same per-unit AND-reduce as the other status lines, so
// the synthesized /RDY of the physical unit (motor off = ready for the
// drive-ID protocol; motor on = real spin-up gate) substitutes cleanly. The
// vrdy_n vector reproduces the original drives-count gating bit-exactly.
//assign _ready   = (_sel[3] | ~(drives[1] & drives[0]))
//        & (_sel[2] | ~drives[1])
//        & (_sel[1] | ~(drives[1] | drives[0]))
//        & (_sel[0]);                          // (original)
wire [3:0] vrdy_n = { ~(drives[1] & drives[0]), ~drives[1], ~(drives[1] | drives[0]), 1'b0 };
wire [3:0] rdy_src_n = (~phys_mask & vrdy_n) | (phys_mask & {4{phys_ready_n}});
assign _ready = &(_sel | rdy_src_n);

// MiSTer2MEGA65 (AExp Amiga 500 port), July 2026: export the per-unit motor
// latches. The entry of the physical unit drives the real MOTEA pin: a PC
// mechanism has a dedicated per-drive motor line, and the latch is the state
// a real Amiga drive keeps internally after the /SEL-edge motor protocol.
assign motor_on_o = motor_on;

//--------------------------------------------------------------------------------------

//disk data byte and status read
//
// MiSTer2MEGA65 (AExp Amiga 500 port), August 2026: DSKBYTR observation
// surface (see the obs_word/obs_stb port comment). obs_gate is true only
// while the physical drive is the selected, motor-on unit and the A/B revert
// bit is clear; then DSKBYTR returns a faithful BYTEREADY / WORDEQUAL / data
// byte synthesized from the reconstructed word stream of the real drive.
// When the gate is low (Disk Image read, no physical drive, or
// obs_legacy=1) the expression is the original constant stub, byte for byte:
//   assign dskbytr = reg_address_in[8:1]==DSKBYTR[8:1] ?
//                    {1'b1,(trackrd|trackwr),dsklen[14],5'b1_0000,8'h00} : 16'h00_00; (original)
wire obs_gate = |(phys_mask & ~_sel & motor_on) & ~obs_legacy;

// The CPU DSKBYTR read access presents its address on the RGA bus for one
// CCK period; obs_rd_end (falling edge of the address match) fires once at
// the end of the access, so a read returns the current byte and only then
// advances to the next: clear-on-read without disturbing the value the CPU
// is latching this cycle.
reg        obs_rd_d   = 1'b0;
wire       obs_rd_lvl = (reg_address_in[8:1]==DSKBYTR[8:1]);
wire       obs_rd_end = obs_rd_d & ~obs_rd_lvl;

reg [15:0] obs_wordq  = 16'h0000;   // the latched word whose two bytes we emit
reg  [7:0] obs_byte   = 8'h00;      // the byte currently presented at DSKBYTR[7:0]
reg        obs_dskbyt = 1'b0;       // BYTEREADY: a byte is waiting to be read
reg        obs_wordeq = 1'b0;       // WORDEQUAL: obs_wordq matches DSKSYNC
reg        obs_have   = 1'b0;       // an emitted byte is still pending a read
reg        obs_lo     = 1'b0;       // 0 = high byte pending, 1 = low byte pending

always @(posedge clk) begin
  obs_rd_d <= obs_rd_lvl;
  if (reset) begin
    obs_wordq <= 16'h0000; obs_byte <= 8'h00;
    obs_dskbyt <= 1'b0; obs_wordeq <= 1'b0; obs_have <= 1'b0; obs_lo <= 1'b0;
  end else if (obs_gate) begin
    if (obs_stb) begin
      // a fresh word from the real drive (arrives at true flux pace).
      // Newest wins if a previous byte was still pending, which cannot happen
      // in the real-time PIO flow (words ~32 us apart, CPU reads in ~2.5 us).
      obs_wordq  <= obs_word;
      obs_wordeq <= (obs_word == dsksync);
      if (wordsync & (obs_word == dsksync)) begin
        // WORDSYNC=1: a real Paula reframes at the sync match and swallows
        // the sync word, so the first byte delivered after WORDEQUAL is the
        // first byte after the sync. Announce WORDEQUAL only and enqueue
        // nothing, so the bytes of the next word become buffer[0]. The
        // Copylock sector routine depends on this: it requires the first
        // stored word to be the MFM-encoded sector index (the word after the
        // sync) and retries forever otherwise. Under WORDSYNC=0 the sync word
        // is delivered like any other (below).
        obs_byte   <= 8'h00;
        obs_dskbyt <= 1'b0;
        obs_have   <= 1'b0;
        obs_lo     <= 1'b0;
      end else begin
        obs_byte   <= obs_word[15:8];   // raw MFM high byte first
        obs_dskbyt <= 1'b1;
        obs_have   <= 1'b1;
        obs_lo     <= 1'b0;
      end
    end else if (obs_rd_end) begin
      // CPU has just read DSKBYTR: clear BYTEREADY, present the next byte.
      if (obs_have & ~obs_lo) begin
        obs_byte   <= obs_wordq[7:0];  // raw MFM low byte second
        obs_dskbyt <= 1'b1;
        obs_lo     <= 1'b1;
      end else begin
        obs_dskbyt <= 1'b0;            // both bytes consumed - wait for next word
        obs_wordeq <= 1'b0;
        obs_have   <= 1'b0;
      end
    end
  end else begin
    // gate closed: park the surface so a later engagement starts clean
    obs_dskbyt <= 1'b0; obs_wordeq <= 1'b0; obs_have <= 1'b0; obs_lo <= 1'b0;
  end
end

wire [15:0] dskbytr_obs = {obs_dskbyt, (trackrd|trackwr), dsklen[14], obs_wordeq, 4'b0000, obs_byte};

assign dskbytr = reg_address_in[8:1]==DSKBYTR[8:1] ?
                 (obs_gate ? dskbytr_obs
                           : {1'b1,(trackrd|trackwr),dsklen[14],5'b1_0000,8'h00})
                 : 16'h00_00;

//disk sync register
always @(posedge clk) begin
  if (clk7_en) begin
  	if (reset) 
  		dsksync[15:0] <= 16'h4489;
  	else if (reg_address_in[8:1]==DSKSYNC[8:1])
  		dsksync[15:0] <= data_in[15:0];
  end
end

//disk length register
// MiSTer2MEGA65 (AExp Amiga 500 port), June 2026: writes go to the split
// registers dsklen_14_0/dsklen_15 (see declaration above).
always @(posedge clk) begin
  if (clk7_en) begin
  	if (reset)
  		dsklen_14_0[14:0] <= 0;
  	else if (reg_address_in[8:1]==DSKLEN[8:1])
  		dsklen_14_0[14:0] <= data_in[14:0];
  	else if (fifo_wr)//decrement length register
  		dsklen_14_0[13:0] <= dsklen[13:0] - 14'd1;
  end
end

//disk length register DMAEN
always @(posedge clk) begin
  if (clk7_en) begin
  	if (reset)
  		dsklen_15 <= 0;
  	else if (blckint)
  		dsklen_15 <= 0;
  	else if (reg_address_in[8:1]==DSKLEN[8:1])
  		dsklen_15 <= data_in[15];
  end
end

//dmaen - disk dma enable signal
always @(posedge clk) begin
  if (clk7_en) begin
  	if (reset)
  		dmaen <= 0;
  	else if (blckint)
  		dmaen <= 0;
  	else if (reg_address_in[8:1]==DSKLEN[8:1])
  		dmaen <= data_in[15] & dsklen[15];//start disk dma if second write in a row with dsklen[15] set
  end
end

//dsklen zero detect
assign lenzero = (dsklen[13:0]==0);

//--------------------------------------------------------------------------------------
//disk data read path
wire	busrd;				//bus read
wire	buswr;				//bus write
reg		trackrdok;			//track read enable

//disk buffer bus read address decode
assign busrd = (reg_address_in[8:1]==DSKDATR[8:1]);

//disk buffer bus write address decode
assign buswr = (reg_address_in[8:1]==DSKDAT[8:1]);

//fifo data input multiplexer
assign fifo_in[15:0] = trackrd ? rx_data[15:0] : data_in[15:0];

//data word transfer strobe
wire stbdat = cmd_fdd && stb7 && &cmd_cnt;

//fifo write control
assign fifo_wr = (trackrdok & stbdat & ~lenzero) | (buswr & dmaon);

//delayed version to allow writing of the last word to empty fifo
always @(posedge clk) begin
  if (clk7_en) begin
  	fifo_wr_del <= fifo_wr;
  end
end

//fifo read control
assign fifo_rd = (busrd & dmaon) | (trackwr & stbdat);

//DSKSYNC interrupt
wire sync_match;
assign sync_match = dsksync[15:0]==rx_data[15:0] && stbdat && trackrd;

assign syncint = sync_match | ~dmaen & |(~_sel & motor_on & disk_present) & sof;

//track read enable / wait for syncword logic
always @(posedge clk) begin
  if (clk7_en) begin
  	if (!trackrd)//reset
  		trackrdok <= 0;
  	else//wordsync is enabled, wait with reading untill syncword is found
  		trackrdok <= ~wordsync | sync_match | trackrdok;
  end
end

// MiSTer2MEGA65 (AExp Amiga 500 port), July 2026: store signature for the
// Hardware Floppy diagnostics (physical_fdd_diag). Per track-read attempt
// (re-armed on the rising edge of trackrd) it XORs the first 1024 words
// written into the read FIFO, with checkpoints after 64 and 256 words and a
// copy of the first 8 words. For the Hardware Floppy the track engine
// serves from the DSKSYNC word on and signs the same 1024 words on its side
// of the io channel. Under WORDSYNC=0, as trackdisk sets it, Paula stores
// from the first served word, so the two windows match and equal signatures
// show that no word was lost or altered in between; under WORDSYNC=1 Paula
// drops the sync word and the windows are one word apart. Diagnostic only:
// nothing functional reads these registers. See AExp's
// doc/developers/hardware-floppy.md, section 8.3 (Reading a dump).
reg [15:0] dsig_acc  = 16'd0;
reg [15:0] dsig_last = 16'd0;
reg [15:0] dsig_c64  = 16'd0;
reg [15:0] dsig_c256 = 16'd0;
reg [127:0] dsig_tap = 128'd0;
reg [10:0] dsig_cnt  = 11'd0;
reg  [7:0] dsig_att  = 8'd0;
reg        dsig_trd  = 1'b0;
always @(posedge clk) begin
  if (clk7_en) begin
    dsig_trd <= trackrd;
    if (trackrd & ~dsig_trd) begin
      dsig_acc  <= 16'd0;
      dsig_cnt  <= 11'd0;
      dsig_c64  <= 16'd0;
      dsig_c256 <= 16'd0;
      dsig_att  <= dsig_att + 8'd1;
    end else if (fifo_wr & ~fifo_full & trackrd & (dsig_cnt != 11'd1024)) begin
      dsig_acc <= dsig_acc ^ rx_data[15:0];
      dsig_cnt <= dsig_cnt + 11'd1;
      if (dsig_cnt < 11'd8)
        dsig_tap[{dsig_cnt[2:0], 4'b0000} +: 16] <= rx_data[15:0];
      if (dsig_cnt == 11'd63)
        dsig_c64 <= dsig_acc ^ rx_data[15:0];
      if (dsig_cnt == 11'd255)
        dsig_c256 <= dsig_acc ^ rx_data[15:0];
      if (dsig_cnt == 11'd1023)
        dsig_last <= dsig_acc ^ rx_data[15:0];
    end
  end
end
assign fdd_dsig  = dsig_last;
assign fdd_datt  = dsig_att;
assign fdd_dc64  = dsig_c64;
assign fdd_dc256 = dsig_c256;
assign fdd_dtap  = dsig_tap;
assign fdd_dws   = wordsync;

assign fifo_reset = reset | ~dmaen;

//disk fifo / trackbuffer
paula_floppy_fifo db1
(
	.clk(clk),
	.clk7_en(clk7_en),
	.reset(fifo_reset),
	.in(fifo_in),
	.out(fifo_out),
	.rd(fifo_rd & ~fifo_empty),
	.wr(fifo_wr & ~fifo_full),
	.empty(fifo_empty),
	.full(fifo_full),
	.cnt(fifo_cnt)
);


//disk data read output gate
assign dskdatr[15:0] = busrd ? fifo_out[15:0] : 16'h00_00;

//--------------------------------------------------------------------------------------
//dma request logic
assign dmal = dmaon & (~dsklen[14] & ~fifo_empty | dsklen[14] & ~fifo_full);

//dmas is active during writes
assign dmas = dmaon & dsklen[14] & ~fifo_full;

//--------------------------------------------------------------------------------------
//main disk controller
reg		[1:0] dskstate;		//current state of disk
reg		[1:0] nextstate; 	//next state of state

//disk states
parameter DISKDMA_IDLE   = 2'b00;
parameter DISKDMA_ACTIVE = 2'b10;
parameter DISKDMA_INT    = 2'b11;

//disk present and write protect status
always @(posedge clk) begin
  if (clk7_en) begin
  	if(reset)
  		{disk_writable[3:0],disk_present[3:0]} <= 8'b0000_0000;
  	else if (rx_data[15:12]==4'b0001 && stb7 && !cmd_cnt)
  		{disk_writable[3:0],disk_present[3:0]} <= rx_data[7:0];
  end
end

//disk activity LED
assign fdd_led = (dskstate!=DISKDMA_IDLE);
//assign disk_led = |motor_on;

//main disk state machine
always @(posedge clk) begin
  if (clk7_en) begin
  	if (reset)
  		dskstate <= DISKDMA_IDLE;		
  	else
  		dskstate <= nextstate;
  end
end

always @(*) begin
	case(dskstate)
		DISKDMA_IDLE://disk is present in flash drive
		begin
			trackrd = 0;
			trackwr = 0;
			dmaon = 0;
			blckint = 0;
			if (cmd_fdd && stb7 && cmd_cnt==1 && dmaen && !lenzero && enable)//dsklen>0 and dma enabled, do disk dma operation
				nextstate = DISKDMA_ACTIVE; 
			else
				nextstate = DISKDMA_IDLE;			
		end
		DISKDMA_ACTIVE://do disk dma operation
		begin
      trackrd = ~lenzero & ~dsklen[14]; // track read (disk->ram)
      trackwr = dsklen[14]; // track write (ram->disk)
      dmaon = ~lenzero | ~dsklen[14];
			blckint=0;
			if (!dmaen || !enable)
				nextstate = DISKDMA_IDLE;
			else if (lenzero && fifo_empty && !fifo_wr_del)//complete dma cycle done
				nextstate = DISKDMA_INT;
			else
				nextstate = DISKDMA_ACTIVE;			
		end
		DISKDMA_INT://generate disk dma completed (DSKBLK) interrupt
		begin
			trackrd = 0;
			trackwr = 0;
			dmaon = 0;
			blckint = 1;
			nextstate = DISKDMA_IDLE;			
		end
		default://we should never come here
		begin
			trackrd = 1'bx;
			trackwr = 1'bx;
			dmaon = 1'bx;
			blckint = 1'bx;
			nextstate = DISKDMA_IDLE;			
		end
	endcase
end


endmodule

